

//Create pool of messages

if(!use_all_frames){ //Specified subset of availabe send messages
  static_for(spec_msg:root.used_frames.content()){
    static_for(e:root.dbc_msg){
    //TP removed .content()
    /* X.content() returns the content of all nodes in the node set denoted ny X:
       
     Given: 
       
     A{1;};
     A{2;};
     A{3;};
       
     static_for(e:root.A.content()) {e}
       
     results in:
       
     1;2;3;
       
     static_for(e:root.A) {e}
       
     results in:
       
       
     A{1;};
     A{2;};
     A{3;};
    */
      //TP print(e.id.content(), hd(spec_msg), "\n");
      //TP print is not evaluated at compile time, and will be ignored in any later phase.
      
      if(text(hd(e.id.content())) == text(spec_msg)){ 
        dbc_msg_pool{
          e.content();
        };
      };
    };
  };

}
else //All available messages.
{ 
  static_for(e:root.dbc_msg){
    dbc_msg_pool{
      e.content();
    };
  };
  
}

static_for(e:root.dbc_msg_pool){
  val h = text(e.sender.content());
  make_struct("__msgs_by_sender_"+h,text(e.id.content()));
}

sender_list_temp{
  static_for(e:root.dbc_msg_pool){
   val h = text(e.sender.content());
   h;
  }
};
sender_list{
  root.sender_list_temp.content().sort().unique();     
};

cycles_temp{
  static_for(e:root.dbc_cycle_def){
    e.interval.content();
  }
};

cycles_list{
  root.cycles_temp.content().sort().unique();     
};

static_for(e:root.dbc_cycle_def){
 make_struct("cycle_"+text(e.interval.content()),hd(e.canid.content()) );   
}

static_for(e:root.dbc_msg_pool){
  make_struct("canid2id_"+text(e.canid.content()),text(e.id.content()));   
}

static_for(e: root.sender_list.content()){
 frame_group{
  can_node{e;};
  send_frames{
    static_for(ee:root.cycles_list.content()){
      interval_group{
        interval{ee;};
        frames{
          static_for(eee:root.select("cycle_"+text(ee)).content()){
            val z = text(root.select("canid2id_"+text(eee)).content());
            static_for(ee: root.select("__msgs_by_sender_"+text(e)).content()){
             if(text(ee)==z) z;
            }
          }
        };
      };
    }
  };
 };
}

static_for(e:root.frame_group){
     static_for(ee:e.send_frames.interval_group){
       static_for(eee:ee.frames.content()){
       make_struct("do_"+text(ee.interval.content())+"_"+text(e.can_node.content()),
       send(as_identifier(eee),as_identifier( text(e.can_node.content()) +"_out") ) );
       }
     }
}

Systemstate init_timer_already_called;
init_timer_already_called = 0;
global_functions{
 init_timer{
   if(init_timer_already_called) return 0;
   init_timer_already_called = 1;
   static_for(e:root.cycles_list.content()){
     start_periodic_timer( (hd(e) / 1000.0 ) *s, as_symbol("ev_"+text(hd(e))+"_ms_elapsed","Event") );
   }
 };
};


static_for(e:root.frame_group){
 sm{
   as_identifier("sender_"+text(e.can_node.content()));
   cover{edges_upto_1;};
   states{
     Initial;     
   };
   on_enter{
     init_timer();
   };

   Actions{
     static_for(ee:e.send_frames.interval_group){       
       make_struct("do_"+text(ee.interval.content() ), 
         root.select("do_"+text(ee.interval.content())+"_"+text(e.can_node.content())).content()
        );
     }
   };
   static_for(ee:e.send_frames.interval_group){
     t{Initial;
       Initial;
       as_symbol("ev_"+text(ee.interval.content())+"_ms_elapsed","Event");
       as_identifier("do_"+text(ee.interval.content()));
    };
   }
 };
}



//Extract signals from msg pool
static_for(e:root.dbc_msg_pool.sig){
    as_symbol(text(e.name.content()),"Systemstate") = strip(e.min.content());
    reg_as_symbol_with_global_scope(text(e.name.content()),"Systemstate");
    constraints{
      as_symbol(text(e.name.content()),"Systemstate") >= strip(e.min.content());
      strip(e.max.content()) >= as_symbol(text(e.name.content()),"Systemstate");
    };
}

//Build frames from msg pool
static_for(e:root.dbc_msg_pool){
    frame{
     e.id;
     comment{
      root.select("dbc_signal_comment" + text(e.canid.content())).content();      
     };
     value_mappings{
      root.select("dbc_value_description_mapping" + text(e.canid.content())).content();
     };

     data{
      static_for(sig : e.sig.sort("start")){
        val content = as_symbol(text(sig.name.content()),"Systemstate");
        val type = "uint"+text(sig.width.content());
        in{make_struct(type,
         content            
        ); };
        out{make_struct(type,
         content            
        ); };
        
        if (!last){
           val first_free_bit_after = strip(sig.start.content()) + strip(sig.width.content());
           val next_occupied_bit = strip(next.start.content());
           val gap = next_occupied_bit - first_free_bit_after;
           if (gap > 0){
               make_struct("uint"+text(gap),0);               
           }
        }
      }
      
     };
    };
}
static_for(e:root.dbc_msg_pool){
    static_for(h:root.select("dbc_value_description_mapping" + text(e.canid.content()))){
        xpartition{
            of{h.sig.name.content();};
            static_for(entry:h.sig.entry){
                {as_symbol(text(strip(h.sig.name.content())),"Systemstate") == strip(entry.index.content());as_identifier(strip(entry.value.content()));}
            }
        };
    }
}
dbc_import_internal_sender_list1{
  root.dbc_msg_pool.sender.content().sort().unique();     
};

dbc_import_internal_sender_list2{
  root.dbc_import_internal_sender_list1.content();
  root.dbc_sender_ofmsg.sender.content().sort().unique();  
};

dbc_import_internal_sender_list3{
  root.dbc_import_internal_sender_list2.content().sort().unique();   
};

static_for(e:root.dbc_import_internal_sender_list3.content()){
if (text(e) != "Vector__XXX"){
 dbc_import_internal_sender_info{
  id{as_identifier(text(e));};    
  msgs{
   static_for(f:root.dbc_msg_pool){
     if (text(f.sender.content()) == text(e)){
         f.canid.content();
     }
   }
   static_for(ee:root.dbc_sender_ofmsg){
    static_for(eee:ee.sender.content()){
    if (text(eee) == text(e)){
      ee.can_id.content();
    }       
    }
   }
 };
 };
}
}


dbc_import_internal_sender_list{
  static_for(e:root.dbc_import_internal_sender_list1.content()){
   if (text(e) != "Vector__XXX"){
       e;
   }    
  }  
};

static_for(e:root.dbc_import_internal_sender_list3.content()){
 if ( text(e) != "Vector__XXX") {
    sender
    {
    id { as_identifier(text(e)+"_out"); };
    transport {
    canbus {
        extended;
        //bus_id { "vcan0"; };
        can_id_mapping{
         static_for(f:root.dbc_import_internal_sender_info){
            if (text(f.id.content()) == text(e)){
              static_for(g:f.msgs.content().sort().unique()){
                 static_for(msg:root.dbc_msg_pool){                     
                     if (text(msg.canid.content()) == text(g)){
                        msg.id.content();g; 
                    }
                }
              }
            }
         }        
        };
    };
    };
    };
 }
}


