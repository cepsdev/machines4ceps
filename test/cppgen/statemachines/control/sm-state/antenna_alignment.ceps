Event ev_antenna_alignment_changed, ev_antenna_alignment_status_changed, ev_antenna_is_levelled_out, ev_antenna_to_level_out_is_not_necessary;
Event ev_slide_detector_slide_alarm;
Event ev_z_drive_move, ev_z_drive_movement_stopped;

Statemachine
{
  id  { sm_Antenna_alignment; };
  States  { Initial;  Aligned; Not_Aligned; Dont_Care;  Moving; Leveling; };

  Actions
  {
    a_antenna_alignment_dont_care
    {
      ANTENNA_ALIGNMENT_STATUS = ANTENNA_DONT_CARE;
      ivenet_platform_alignment_state.value = IVENET_PLATFORM_IS_NOT_ALIGNED;
      ev_antenna_alignment_status_changed ( ANTENNA_DONT_CARE );
    };
    a_antenna_is_aligned
    {
      ANTENNA_ALIGNMENT_STATUS = ANTENNA_IS_ALIGNED;
      ivenet_platform_alignment_state.value = IVENET_PLATFORM_IS_ALIGNED;
      ev_antenna_alignment_status_changed ( ANTENNA_IS_ALIGNED );
    };
    a_antenna_is_not_aligned
    {
      ANTENNA_ALIGNMENT_STATUS = ANTENNA_IS_NOT_ALIGNED;
      ivenet_platform_alignment_state.value = IVENET_PLATFORM_IS_NOT_ALIGNED;
      ev_antenna_alignment_status_changed ( ANTENNA_IS_NOT_ALIGNED );
    };
    a_antenna_not_alignment_due_to_mast_move
    {
      ANTENNA_ALIGNMENT_STATUS = ANTENNA_IS_NOT_ALIGNED;
      ivenet_platform_alignment_state.value = IVENET_PLATFORM_IS_NOT_ALIGNED_DUE_TO_MAST_MOVEMENT;
      ev_antenna_alignment_status_changed ( ANTENNA_IS_NOT_ALIGNED );
    };
    a_antenna_not_alignment_due_to_vehicle_move
    {
      ANTENNA_ALIGNMENT_STATUS = ANTENNA_IS_NOT_ALIGNED;
      ivenet_platform_alignment_state.value = IVENET_PLATFORM_IS_NOT_ALIGNED_DUE_TO_VEHICLE_MOVEMENT;
      ev_antenna_alignment_status_changed ( ANTENNA_IS_NOT_ALIGNED );
    };
    a_antenna_leveling_started
    {
      ANTENNA_ALIGNMENT_STATUS = ANTENNA_IS_NOT_ALIGNED;
      ivenet_platform_alignment_state.value = IVENET_PLATFORM_ALIGNMENT_IS_IN_PROGRESS;
      ev_antenna_alignment_status_changed ( ANTENNA_IS_NOT_ALIGNED );
    };
  };

  Transition  { Initial;     Dont_Care;                                              !g_antenna_in_working_range;                                 a_antenna_alignment_dont_care;  };
  Transition  { Initial;     Not_Aligned;                                             g_antenna_in_working_range  && !g_antenna_is_levelled_out;  a_antenna_is_not_aligned;  };
  Transition  { Initial;         Aligned;                                             g_antenna_in_working_range  &&  g_antenna_is_levelled_out;  a_antenna_is_aligned;  };

  Transition  { Dont_Care;   Moving;      ev_z_drive_move;                            g_antenna_in_working_range;                                 a_antenna_not_alignment_due_to_mast_move; };
  Transition  { Dont_Care;   Not_Aligned;                                             g_antenna_in_working_range  && !g_antenna_is_levelled_out;  a_antenna_is_not_aligned; };
  Transition  { Dont_Care;       Aligned; ev_antenna_is_levelled_out;                 g_antenna_in_working_range  &&  g_antenna_is_levelled_out;  a_antenna_is_aligned; };
  Transition  { Dont_Care;       Aligned; ev_antenna_to_level_out_is_not_necessary;   g_antenna_in_working_range  &&  g_antenna_is_levelled_out;  a_antenna_is_aligned; };

  Transition  { Moving;      Dont_Care;                                              !g_antenna_in_working_range;                                 a_antenna_alignment_dont_care; };
  Transition  { Moving;      Not_Aligned; ev_z_drive_movement_stopped;                g_antenna_in_working_range  && !g_antenna_is_levelled_out;  a_antenna_is_not_aligned; };
  Transition  { Moving;          Aligned; ev_z_drive_movement_stopped;                g_antenna_in_working_range  &&  g_antenna_is_levelled_out;  a_antenna_is_aligned; };

  Transition  { Not_Aligned; Dont_Care;                                              !g_antenna_in_working_range;                                 a_antenna_alignment_dont_care;  };
  Transition  { Not_Aligned; Moving;      ev_z_drive_move;                            g_antenna_in_working_range;                                 a_antenna_not_alignment_due_to_mast_move; };
  Transition  { Not_Aligned; Leveling;    ev_antenna_to_level_out_started;                                                              a_antenna_leveling_started; };
  Transition  { Not_Aligned;     Aligned; ev_antenna_is_levelled_out;                 g_antenna_in_working_range  &&  g_antenna_is_levelled_out;  a_antenna_is_aligned; };
  Transition  { Not_Aligned;     Aligned; ev_antenna_to_level_out_is_not_necessary;   g_antenna_in_working_range  &&  g_antenna_is_levelled_out;  a_antenna_is_aligned; };

  Transition  {     Aligned; Dont_Care;                                              !g_antenna_in_working_range;                                 a_antenna_alignment_dont_care;  };
  Transition  {     Aligned; Moving;      ev_z_drive_move;                            g_antenna_in_working_range;                                 a_antenna_not_alignment_due_to_mast_move; };
//  Transition  {     Aligned; Not_Aligned; ev_antenna_alignment_changed;                                            !g_antenna_is_levelled_out;  a_antenna_is_not_aligned; };  //  ! transition only with slide_detector !
  Transition  {     Aligned; Not_Aligned; ev_slide_detector_slide_alarm;                                                                          a_antenna_not_alignment_due_to_vehicle_move; };

  Transition  { Leveling;        Aligned; ev_antenna_alignment_changed;                                               g_antenna_is_levelled_out;  a_antenna_is_aligned; };
  Transition  { Leveling;    Not_Aligned; ev_antenna_alignment_changed;                                              !g_antenna_is_levelled_out;  a_antenna_is_not_aligned; };
};

