Event ev_y_drive_stop;
Event ev_y_drive_movement_stopped, ev_y_drive_reset, ev_y_drive_move_up;
Event ev_y_drive_prepares_to_move;
Event ev_y_drive_move;
Event ev_y_drive_can_control, ev_y_drive_can_state_response;


global_functions
{
  y_drive_set_soll_position
  {
    params{ inSollPos; inSollGeschw; };
    print ( "-------- y_drive_set_soll_position -------- inSollPos: ", inSollPos, ", inSollGeschw: ", inSollGeschw, "\n" );
    y_drive.Control.ANTRIEB_MODE_DEFAULT  = DRIVE_OPERATIONAL_MODE_POSITION;

    if ( inSollPos > y_drive.MAX_POS )
    {
      y_drive.SollPos = y_drive.MAX_POS;
    } else
    {
      if ( inSollPos < y_drive.MIN_POS )
      {
        y_drive.SollPos = y_drive.MIN_POS;
      } else
      {
        y_drive.SollPos = inSollPos;
      };
    };
    y_drive.SollGeschw = inSollGeschw;
    print ( "-------------------------------- Y_drive_move.SollPos:    ", y_drive.SollPos, ", SollGeschw: ", y_drive.SollGeschw, "\n" );
  };

  y_drive_set_target_speed
  {
    params{ inSollGeschw; };
    print ( "-------- y_drive_set_target_speed -------- inSollGeschw: ", inSollGeschw, "\n" );
    y_drive.Control.ANTRIEB_MODE_DEFAULT  = DRIVE_OPERATIONAL_MODE_SPEED;

    if ( inSollGeschw >= 0 )
    {
      y_drive.SollPos = y_drive.MAX_POS;
      y_drive.VorzeichenSpeed = 1;
    };
    if ( inSollGeschw < 0 )
    {
      y_drive.SollPos = y_drive.MIN_POS;
      y_drive.VorzeichenSpeed = -1;
    };
    if ( inSollGeschw == 0 )
    {
      y_drive.SollGeschw = y_drive.SPEED_MAX;
    } else
    {
      y_drive.SollGeschw = y_drive.VorzeichenSpeed * y_drive.SollGeschwAbsolut;
    };
  };
};

Statemachine
{
  val MOVEMENT_SELECTION_IDX          = 1;
  val TARGET_VALUE_IDX                = 2;

  id  { sm_Y_drive_move;  };
  description { "Y_drive_move"; };
  States  { Initial;  Final;  Error;  Moving; SlowDown;  };

  import  { sm_Y_drive_enable;  };
  import  { sm_Y_drive_disable; };
  import  { sm_Y_drive_brake_release; };
  import  { sm_Y_drive_brake_apply; };

  Actions
  {
    a_y_drive_movement_stopped
    {
      //print("-------- ev_y_drive_movement_stopped --------\n");

      ev_y_drive_movement_stopped;
    };
    a_y_drive_movement_start
    {
      print ( "-------- ev_y_drive_move --------\n" );

      y_drive.Control.ANTRIEB_MODE          = y_drive.Control.ANTRIEB_MODE_DEFAULT;

      ev_y_drive_can_control;
      ev_y_drive_move;
    };
    a_y_drive_stop_moving
    {
      y_drive.Control.ANTRIEB_MODE          = DRIVE_OPERATIONAL_MODE_STOP;
      ev_y_drive_can_control;
    };
    a_y_drive_slow_down
    {
      print ( "-------- a_y_drive_slow_down --------\n" );
      y_drive.Control.ANTRIEB_MODE          = DRIVE_OPERATIONAL_MODE_SPEED;
      y_drive.SollGeschw = 0;
      ev_y_drive_can_control;
    };
  };

  on_enter
  {
    if ( argc() < 2 )
    {
      print ( "---------------------------------------- Too few arguments for start of 'sm_Y_drive_move' \n" );
    } else
    {
      if ( argv ( MOVEMENT_SELECTION_IDX ) == MODE_TO_TARGET )
      {
        y_drive_set_soll_position ( argv ( TARGET_VALUE_IDX ), y_drive.SPEED_DEFAULT );
      } else
      if ( argv ( MOVEMENT_SELECTION_IDX ) == MODE_GIVEN_SPEED )
      {
        y_drive_set_target_speed ( argv ( TARGET_VALUE_IDX ) );
      };
    };
    y_drive.Control.ANTRIEB_MODE          = DRIVE_OPERATIONAL_MODE_STOP;
  };

  on_exit
  {
    y_drive.Control.ANTRIEB_MODE          = DRIVE_OPERATIONAL_MODE_STOP;
    ev_y_drive_stop;
    ev_y_drive_can_control;
  };

  Transition  { Initial;                        sm_Y_drive_enable;                                                                          };
  Transition  { sm_Y_drive_enable.Final;        sm_Y_drive_brake_release;                                                                   };
  Transition  { sm_Y_drive_enable.Error;        Error;                                                                                      };
  Transition  { sm_Y_drive_brake_release.Final; Moving;                                                     a_y_drive_movement_start;       };
  Transition  { sm_Y_drive_brake_release.Error; Error;                                                                                      };
  Transition  { Moving;                         sm_Y_drive_brake_apply;     ev_y_drive_can_state_response;  a_y_drive_stop_moving;          g_y_drive_reach_inpos;    };
  Transition  { Moving;                         SlowDown;                   ev_y_drive_can_state_response;  a_y_drive_slow_down;           !g_y_drive_reach_inpos &&  g_y_drive_is_in_soll_position;  };
  Transition  { Moving;                         Error;                      ev_y_drive_can_state_response;                                 !g_y_drive_reach_inpos && !g_y_drive_is_in_soll_position && g_y_drive_is_not_moving;  };
  Transition  { Moving;                         SlowDown;                   ev_y_drive_stop;                a_y_drive_slow_down;            };
  Transition  { SlowDown;                       sm_Y_drive_brake_apply;     ev_y_drive_can_state_response;  a_y_drive_stop_moving;          g_y_drive_is_slow_down;   };
  Transition  { sm_Y_drive_brake_apply.Final;   sm_Y_drive_disable;                                                                         };
  Transition  { sm_Y_drive_brake_apply.Error;   Error;                                                                                      };
  Transition  { sm_Y_drive_disable.Final;       Final;                                                      a_y_drive_movement_stopped;     };
  Transition  { sm_Y_drive_disable.Error;       Error;                                                                                      };
};

