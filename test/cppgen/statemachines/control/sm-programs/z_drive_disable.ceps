Event ev_z_drive_disable_starting,  ev_z_drive_disable, ev_z_drive_is_disabled;
Event ev_z_drive_disable_timeout,   ev_z_drive_disable_was_interrupted, ev_z_drive_disable_interrupted;
Event ev_z_drive_can_control, ev_z_drive_can_state_response;
Guard g_z_drive_has_antrieb_fault;

Statemachine
{
  id  { sm_Z_drive_disable; };
  description { "Z_drive_disable";  };
  States  { Initial;  Final;  Start;  Error;  Disabling;  };

  Actions
  {
    a_z_drive_disable_not_needed  { ev_z_drive_is_disabled;  };
    a_z_drive_disable_starting    { ev_z_drive_disable_starting;  };
    a_z_drive_disable_start
    {
      print ( "---------- Z_drive disable ----------\n" );
      z_drive.Control.ANTRIEB_ENABLE_IN     = 0;
      ev_z_drive_can_control;
      ev_z_drive_disable;
      a_z_drive_set_timeout_timer;
      //ev_z_drive_disable_timeout; //  force timeout only for test
    };
    a_z_drive_is_disabled
    {
      a_z_drive_stop_timeout_timer;
      print ( "---------- Z_drive is_disabled ----------\n" );
      ev_z_drive_is_disabled;
    };
    a_z_drive_set_timeout_timer   { start_timer ( DEF_TIME_RUCKMLD_ANTRIEBE * s, ev_z_drive_disable_timeout,  timer_z_drive_disable );  };
    a_z_drive_was_interrupted     { a_z_drive_stop_timeout_timer; ev_z_drive_disable_was_interrupted; };
    a_z_drive_disable_timeout     { ev_z_drive_disable_interrupted; };
    a_z_drive_stop_timeout_timer  { stop_timer ( timer_z_drive_disable ); };
  };

  on_exit
  {
    if ( !in_state ( Final ) )
    {
      a_z_drive_was_interrupted;
    };
  };

  Transition  { Initial;      Final;                                       !g_z_drive_is_enabled;         a_z_drive_disable_not_needed; };
  Transition  { Initial;      Start;                                        g_z_drive_is_enabled;         a_z_drive_disable_starting;   };
  Transition  { Start;        Disabling;                                   !g_z_drive_has_antrieb_fault;  a_z_drive_disable_start;      };
  Transition  { Disabling;    Final;      ev_z_drive_can_state_response;   !g_z_drive_is_enabled;         a_z_drive_is_disabled;        };
  Transition  { Disabling;    Error;      ev_z_drive_disable_timeout;                                     a_z_drive_disable_timeout;    };
};

