Event ev_z_drive_enable_starting,   ev_z_drive_enable,  ev_z_drive_is_enabled;
Event ev_z_drive_enable_timeout,    ev_z_drive_enable_was_interrupted,  ev_z_drive_enable_interrupted;
Event ev_z_drive_can_control, ev_z_drive_can_state_response;
Guard g_z_drive_has_antrieb_fault;

Statemachine
{
  id  { sm_Z_drive_enable;  };
  description { "Z_drive_enable";   };
  States  { Initial;  Final;  Start;  Error;  Enabling;   };

  Actions
  {
    a_z_drive_enable_not_needed   { ev_z_drive_is_enabled;  };
    a_z_drive_enable_starting     { ev_z_drive_enable_starting;   };
    a_z_drive_enable
    {
      print ( "---------- Z_drive enable ----------\n" );
      z_drive.Control.ANTRIEB_ENABLE_IN     = 1;
      ev_z_drive_can_control;
      ev_z_drive_enable;
      a_z_drive_set_timeout_timer;
      //ev_z_drive_enable_timeout;  //  force timeout only for test
    };
    a_z_drive_is_enabled
    {
      a_z_drive_stop_timeout_timer;
      print ( "---------- Z_drive is_enabled ----------\n" );
      ev_z_drive_is_enabled;
    };
    a_z_drive_set_timeout_timer   { start_timer ( DEF_TIME_RUCKMLD_ANTRIEBE * s, ev_z_drive_enable_timeout,   timer_z_drive_enable ); };
    a_z_drive_was_interrupted     { a_z_drive_stop_timeout_timer;   ev_z_drive_enable_was_interrupted; };
    a_z_drive_enable_timeout      { ev_z_drive_enable_interrupted;  };
    a_z_drive_stop_timeout_timer  { stop_timer ( timer_z_drive_enable ); };
  };

  on_exit
  {
    if ( !in_state ( Final ) )
    {
      a_z_drive_was_interrupted;
    };
  };

  Transition  { Initial;      Final;                                        g_z_drive_is_enabled;         a_z_drive_enable_not_needed;  };
  Transition  { Initial;      Start;                                       !g_z_drive_is_enabled;         a_z_drive_enable_starting;    };
  Transition  { Start;        Enabling;                                    !g_z_drive_has_antrieb_fault;  a_z_drive_enable;             };
  Transition  { Enabling;     Final;      ev_z_drive_can_state_response;    g_z_drive_is_enabled;         a_z_drive_is_enabled;         };
  Transition  { Enabling;     Error;      ev_z_drive_enable_timeout;                                      a_z_drive_enable_timeout;     };
};

