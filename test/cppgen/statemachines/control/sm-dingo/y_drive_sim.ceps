// Y_drive
Event ev_y_drive_position_changed, ev_y_drive_sim_state_response;
Event ev_y_drive_sim_control; //  see file: rx-dingo.ceps

//  local events and guards
Event ev_y_drive_periodic_moving_timer;

Guard g_y_drive_is_operational, g_y_drive_enable_request, g_y_drive_fault, g_y_drive_control_stop, g_y_drive_brake_release_request;
Guard g_y_drive_speed_mode_request, g_y_drive_position_mode_request, g_y_drive_stop_request, g_y_drive_mode_request_not_used;

Systemstate y_drive_pos_diff, y_drive_speed_diff, y_drive_target_speed, y_drive_target_speed_diff;
Systemstate y_drive_current_braking_distance;  //  braking distance without acceleration
Systemstate y_drive_next_braking_distance_wa;  //  next braking distance with acceleration
Systemstate y_drive_acceleration, y_drive_acceleration_delay;

Globals
{
  g_y_drive_is_operational              =   in_state ( sm_Y_drive_fue.Operational );
  g_y_drive_fault                       = 0;
  g_y_drive_enable_request              = ( y_drive.Control.ANTRIEB_ENABLE_IN == 1 );
  g_y_drive_brake_release_request       = ( y_drive.Control.ANTRIEB_BRAKE_RELEASE == 1 );
  g_y_drive_speed_mode_request          = ( y_drive.Control.ANTRIEB_MODE == DRIVE_OPERATIONAL_MODE_SPEED );
  g_y_drive_position_mode_request       = ( y_drive.Control.ANTRIEB_MODE == DRIVE_OPERATIONAL_MODE_POSITION );
  g_y_drive_stop_request                = ( y_drive.Control.ANTRIEB_MODE == DRIVE_OPERATIONAL_MODE_STOP );
  g_y_drive_mode_request_not_used       = !g_y_drive_stop_request && !g_y_drive_speed_mode_request && !g_y_drive_position_mode_request;
};

Statemachine
{
  id  { sm_Y_drive_sim_move; };
  description { "Y_drive_sim_move"; };
  States  { Initial;  Stoped; MovingSpeed; MovingPosition;  Inpos;  };

  Actions
  {
    a_y_drive_start_moving_speed_mode
    {
      print ( "-------- Y_drive_sim start speed mode --------\n" );
      print ( "-------- Y_drive_sim starts moving with SollGeschw ( MKS / CAN ): ", y_drive.SollGeschw, " / ", y_drive.CanSollGeschw, "\n" );
      print ( "-------- Y_drive_sim ------------------ IstPos / IstGeschw (CAN): ", y_drive.CanIstPos,  " / ", y_drive.CanIstGeschw, "\n" );
      y_drive.Status.ANTRIEB_INPOS    = 0;

      if ( ( y_drive.CanSollGeschw != 0 )
        || ( y_drive.CanIstGeschw != 0 )
        )
      {
        y_drive_target_speed  = y_drive.CanSollGeschw;
        start_periodic_timer ( 0.01 * s, ev_y_drive_periodic_moving_timer, timer_y_drive_moving );
      } else
      {
        print ( "-------- Y_drive_sim SollGeschw == 0 ... do nothing" );
      };
    };
    a_y_drive_start_moving_position_mode
    {
      print ( "-------- Y_drive_sim start position mode --------\n" );
      print ( "-------- Y_drive_sim starts moving to SollPos    ( MKS / CAN ): ", y_drive.SollPos,    " / ", y_drive.CanSollPos, "\n" );
      print ( "-------- Y_drive_sim ---------------- IstPos / IstGeschw (CAN): ", y_drive.CanIstPos,  " / ", y_drive.CanIstGeschw, "\n" );

      if ( y_drive.CanSollPos != y_drive.CanIstPos )
      {
        y_drive.Status.ANTRIEB_INPOS    = 0;

        a_y_drive_calculate_target_speed;
        start_periodic_timer ( 0.01 * s, ev_y_drive_periodic_moving_timer, timer_y_drive_moving );
      } else
      {
        print ( "-------- Y_drive_sim CanSollPos == CanIstPos ... do nothing" );
      };
    };
    a_y_drive_calculate_target_speed
    {
      if ( y_drive.CanIstGeschw >= 0 )  y_drive_acceleration  = ( 0 + y_drive.CAN_ACCELERATION_MAX );
      else                              y_drive_acceleration  = ( 0 - y_drive.CAN_ACCELERATION_MAX );

      y_drive_pos_diff  = y_drive.CanSollPos - y_drive.CanIstPos;
      y_drive_acceleration_delay  = y_drive.CanIstGeschw * 1.001 / y_drive_acceleration;  //  faktor 1.001 prevent cut to integer
      y_drive_current_braking_distance  = truncate ( y_drive_acceleration_delay * y_drive.CanIstGeschw / 2 );
      y_drive_next_braking_distance_wa  = truncate ( 0.5 * ( 1 + y_drive_acceleration_delay ) * ( y_drive.CanIstGeschw + ( 1 * y_drive_acceleration ) ) );
//       print ( "-------- Y_drive_sim ----- calc ----- CanIstPos / CanSollPos / pos_diff: ", y_drive.CanIstPos, " / ", y_drive.CanSollPos, " / ", y_drive_pos_diff, "\n" );
//       print ( "-------- Y_drive_sim ----- calc ----- acceleration_delay / acceleration: ", y_drive_acceleration_delay, " / ", y_drive_acceleration, "\n" );
//       print ( "-------- Y_drive_sim ----- calc ----- braking_distance   current / next: ", y_drive_current_braking_distance, " / ", y_drive_next_braking_distance_wa, "\n" );

      if ( y_drive_pos_diff > 0 )
      {
        if ( y_drive.CanIstGeschw > 0 )
        { //  right direction positiv
          if ( ( y_drive_pos_diff - y_drive.CanIstGeschw - y_drive_next_braking_distance_wa ) >= 0 )
          {
            y_drive_target_speed = y_drive.CanIstGeschw + y_drive_acceleration;
            print ( "-------- Y_drive_sim ++acl ( ", y_drive_acceleration, " ) => ", y_drive_target_speed, "\n");
          } else
          if ( ( y_drive_pos_diff - y_drive.CanIstGeschw - y_drive_current_braking_distance ) >= 0 )
          {
            y_drive_target_speed  = y_drive.CanIstGeschw;
            print ( "-------- Y_drive_sim ++=== ( 0 ) => ", y_drive_target_speed, "\n" );
          } else
          {
            if ( y_drive.CanIstGeschw > y_drive_acceleration )
            {
              y_drive_target_speed  = y_drive.CanIstGeschw - y_drive_acceleration;
              print ( "-------- Y_drive_sim ++brk ( ", ( 0 - y_drive_acceleration ), " ) => ", y_drive_target_speed, "\n");
            } else
            {
              if ( ( 2 * y_drive_pos_diff ) > y_drive.CanIstGeschw )
              {
                y_drive_target_speed  = y_drive_pos_diff - ( y_drive.CanIstGeschw / 2 );
              } else
              {
                y_drive_target_speed  = 0;
              };
              print ( "-------- Y_drive_sim ++brk ( ", y_drive_target_speed - y_drive.CanIstGeschw, " ) -> ", y_drive_target_speed, "\n");
            };
          };
        } else
        if ( y_drive.CanIstGeschw < 0 )
        { //  invert direction
          if ( ( ( y_drive_pos_diff - y_drive.CAN_ACCELERATION_MAX ) <= 0 )
            && ( ( y_drive.CanIstGeschw - y_drive_acceleration ) <= 0 )
            )
          {
            y_drive_target_speed  = y_drive_acceleration / 3;
            print ( "-------- Y_drive_sim +-low ( ", y_drive_target_speed - y_drive.CanIstGeschw, " ) -> ", y_drive_target_speed, "\n");
          } else
          {
            y_drive_target_speed  = y_drive.CanIstGeschw - y_drive_acceleration;
            print ( "-------- Y_drive_sim +-inv ( ", y_drive_acceleration, " ) -> ", y_drive_target_speed,  "\n");
          };
        } else
        { //  zero speed
          if ( y_drive_pos_diff > y_drive.CAN_ACCELERATION_MAX )
          { //  positiv startup
            y_drive_target_speed  = y_drive.CAN_ACCELERATION_MAX;
          } else
          { //  positiv low distance
            y_drive_target_speed  = y_drive_pos_diff;
          };
          print ( "-------- Y_drive_sim 0+ini ( ", y_drive_target_speed - y_drive.CanIstGeschw, " ) => ", y_drive_target_speed, "\n" );
        };
      } else if ( y_drive_pos_diff < 0 )
      {
        if ( y_drive.CanIstGeschw < 0 )
        { //  right direction negativ
          if ( ( y_drive_pos_diff - y_drive.CanIstGeschw - y_drive_next_braking_distance_wa ) <= 0 )
          {
            y_drive_target_speed = y_drive.CanIstGeschw + y_drive_acceleration;
            print ( "-------- Y_drive_sim --acl ( ", y_drive_acceleration, " ) => ", y_drive_target_speed,  "\n");
          } else
          if ( ( y_drive_pos_diff - y_drive.CanIstGeschw - y_drive_current_braking_distance ) <= 0 )
          {
            y_drive_target_speed  = y_drive.CanIstGeschw;
            print ( "-------- Y_drive_sim --=== ( 0 ) => ", y_drive_target_speed, "\n" );
          } else
          {
            if ( y_drive.CanIstGeschw < y_drive_acceleration )
            {
              y_drive_target_speed  = y_drive.CanIstGeschw - y_drive_acceleration;
              print ( "-------- Y_drive_sim --brk ( ", ( 0 - y_drive_acceleration ), " ) => ", y_drive_target_speed,  "\n");
            } else
            {
              if ( ( 2 * y_drive_pos_diff ) < y_drive_acceleration )
              {
                y_drive_target_speed  = y_drive_pos_diff - ( y_drive.CanIstGeschw / 2 );
              } else
              {
                y_drive_target_speed  = 0;
              };
              print ( "-------- Y_drive_sim --brk ( ", y_drive_target_speed - y_drive.CanIstGeschw, " ) -> ", y_drive_target_speed,  "\n");
            };
          };
        } else
        if ( y_drive.CanIstGeschw > 0 )
        { //  invert direction
          if ( ( ( y_drive_pos_diff + y_drive.CAN_ACCELERATION_MAX ) >= 0 )
            && ( ( y_drive.CanIstGeschw - y_drive_acceleration ) >= 0 )
            )
          {
            y_drive_target_speed  = y_drive.CanIstGeschw / 3;
            print ( "-------- Y_drive_sim -+low ( ", y_drive_target_speed - y_drive.CanIstGeschw, " ) -> ", y_drive_target_speed,  "\n");
          } else
          {
            y_drive_target_speed  = y_drive.CanIstGeschw - y_drive_acceleration;
            print ( "-------- Y_drive_sim -+inv ( ", y_drive_acceleration, " ) -> ", y_drive_target_speed,  "\n");
          };
        } else
        { //  zero speed
          if ( y_drive_pos_diff < ( 0 - y_drive.CAN_ACCELERATION_MAX ) )
          { //  negative startup
            y_drive_target_speed  = ( 0 - y_drive.CAN_ACCELERATION_MAX );
          } else
          { //  negative low distance
            y_drive_target_speed  = y_drive_pos_diff;
          };
          print ( "-------- Y_drive_sim 0-ini ( ", y_drive_target_speed - y_drive.CanIstGeschw, " ) => ", y_drive_target_speed, "\n" );
        };
      } else
      {
        if ( abs ( y_drive.CanIstGeschw ) > 0 )
        {
          y_drive_target_speed  = 0;
          print ( "-------- Y_drive_sim ------ on pos zero --- braking distance reached => slow down \n" );
        } else
        {
          y_drive_target_speed  = 0;
          print ( "-------- Y_drive_sim ------ on pos zero with speed zero\n" );
        };
      };

      if ( y_drive_target_speed > ( 0 + y_drive.CAN_SPEED_DEFAULT ) )
      {
        y_drive_target_speed = ( 0 + y_drive.CAN_SPEED_DEFAULT );
        print ( "-------- Y_drive_sim - limit to + CAN_SPEED_DEFAULT\n" );
      };
      if ( y_drive_target_speed < ( 0 - y_drive.CAN_SPEED_DEFAULT ) )
      {
        y_drive_target_speed = ( 0 - y_drive.CAN_SPEED_DEFAULT );
        print ( "-------- Y_drive_sim - limit to - CAN_SPEED_DEFAULT\n" );
      };
    };

    a_y_drive_moving_speed_mode
    {
      print ( "-------- Y_drive_sim speed mode ----- CanIstPos / CanSollGeschw: ", y_drive.CanIstPos, " / ", y_drive.CanSollGeschw, "\n" );
      if ( ( y_drive.CanSollGeschw != 0 )
        || ( y_drive.CanIstGeschw != 0 )
        )
      {
        y_drive_target_speed  = y_drive.CanSollGeschw;
      };
      a_y_drive_moving;
    };

    a_y_drive_moving_position_mode
    {
      print ( "-------- Y_drive_sim position mode -- CanIstPos / CanSollPos / CanIstGeschw: ", y_drive.CanIstPos, " / ", y_drive.CanSollPos, " / ", y_drive.CanIstGeschw, "\n" );

      a_y_drive_calculate_target_speed;
      print ( "-------- Y_drive_sim ---------------- CanIstPos / CanSollPos / CanIstGeschw: ", y_drive.CanIstPos, " / ", y_drive.CanSollPos, " / ", y_drive.CanIstGeschw, "\n" );
      a_y_drive_moving;

      if ( ( abs ( y_drive_pos_diff )     < 3  )
        && ( abs ( y_drive.CanIstGeschw ) < 3  )
        )
      { //  requested position will be reached in this timeframe
        print ( "-------- Y_drive_sim ----- rest ----- CanIstPos / CanSollPos / CanIstGeschw: ", y_drive.CanIstPos, " / ", y_drive.CanSollPos, " / ", y_drive.CanIstGeschw, "\n" );
        y_drive.Status.ANTRIEB_INPOS    = 1;
        print ( "-------- Y_drive_sim ---------------- ANTRIEB_INPOS    = 1\n" );
        y_drive_pos_diff      = 0;
        y_drive_speed_diff    = 0;
        y_drive_target_speed  = 0;
        y_drive.CanIstGeschw  = 0;
        y_drive.CanIstPos     = y_drive.CanSollPos;
        print ( "-------- Y_drive_sim ---------------- CanIstPos / CanSollPos / CanIstGeschw: ", y_drive.CanIstPos, " / ", y_drive.CanSollPos, " / ", y_drive.CanIstGeschw, "\n" );
      };
    };

    a_y_drive_moving
    {
      //  limiter
      if ( y_drive_target_speed > ( 0 + y_drive.CAN_SPEED_MAX ) )
      {
        y_drive_target_speed = ( 0 + y_drive.CAN_SPEED_MAX );
        print ( "-------- Y_drive_sim - limit to + CAN_SPEED_MAX\n" );
      };
      if ( y_drive_target_speed < ( 0 - y_drive.CAN_SPEED_MAX ) )
      {
        y_drive_target_speed = ( 0 - y_drive.CAN_SPEED_MAX );
        print ( "-------- Y_drive_sim - limit to - CAN_SPEED_MAX\n" );
      };

      y_drive_target_speed_diff = y_drive_target_speed - y_drive.CanIstGeschw;
      print ( "-------- Y_drive_sim ---------------- target_speed / CanIstGeschw / target_speed_diff: ", y_drive_target_speed, " / ", y_drive.CanIstGeschw, " / ", y_drive_target_speed_diff, "\n" );
      if ( abs ( y_drive_target_speed_diff ) >=  y_drive.CAN_ACCELERATION_MAX )
      {
        if ( y_drive_target_speed_diff > 0 )
        {
          y_drive_speed_diff =     y_drive.CAN_ACCELERATION_MAX;
        } else
        {
          y_drive_speed_diff = 0 - y_drive.CAN_ACCELERATION_MAX;
        };
      } else
      {
        y_drive_speed_diff = y_drive_target_speed_diff;
        print ( "-------- Y_drive_sim reach target speed\n" );
      };

      y_drive.CanIstPos     = y_drive.CanIstGeschw + ( y_drive_speed_diff / 2 ) + y_drive.CanIstPos;
      y_drive.CanIstGeschw  = y_drive.CanIstGeschw +   y_drive_speed_diff;
      y_drive_pos_diff      = y_drive.CanSollPos - y_drive.CanIstPos;
      print ( "-------- Y_drive_sim moving --------- CanIstPos / CanIstGeschw / speed_diff:  ", y_drive.CanIstPos, " / ", y_drive.CanIstGeschw, " / ", y_drive_speed_diff, "\n" );
      if ( y_drive.CanIstGeschw >= 0 )  { y_drive.CanIstStrom   = 20 + truncate ( (       y_drive.CanIstGeschw   + y_drive_speed_diff ) / 100 ); }
      else                              { y_drive.CanIstStrom   = 20 + truncate ( ( abs ( y_drive.CanIstGeschw ) + y_drive_speed_diff ) / 200 ); };
      ev_y_drive_position_changed;
    };
    a_y_drive_sim_stop_moving
    {
      print ( "-------- Y_drive_sim stops moving --------\n" );
      stop_timer ( timer_y_drive_moving );
      y_drive.Status.ANTRIEB_INPOS    = 0;
    };
    a_y_drive_error_moving
    {
      print ( "-------- Y_drive_sim error moving --------\n" );
      a_y_drive_sim_stop_moving;
    };
    a_y_drive_error_stoped
    {
      print ( "-------- Y_drive_sim error stoped --------\n" );
      a_y_drive_sim_stop_moving;
    };
  };

  on_enter
  {
    //print("-------- Y_drive_sim.on_enter(), SollGeschw: -------- ", y_drive.SollGeschw, "\n");
  };

  on_exit
  {
    if ( !in_state ( Stoped ) )
    {
      a_y_drive_sim_stop_moving;
    };
  };

  Transition  { Initial;        Stoped;         };
  Transition  { Stoped;         Stoped;         ev_y_drive_sim_control;             g_y_drive_mode_request_not_used;  a_y_drive_error_stoped;               };
  Transition  { Stoped;         MovingSpeed;    ev_y_drive_sim_control;             g_y_drive_speed_mode_request;     a_y_drive_start_moving_speed_mode;    };
  Transition  { Stoped;         MovingPosition; ev_y_drive_sim_control;             g_y_drive_position_mode_request;  a_y_drive_start_moving_position_mode; };

  Transition  { MovingSpeed;    MovingSpeed;    ev_y_drive_periodic_moving_timer;   g_y_drive_speed_mode_request;     a_y_drive_moving_speed_mode;          };
  Transition  { MovingSpeed;    MovingPosition; ev_y_drive_periodic_moving_timer;   g_y_drive_position_mode_request;  a_y_drive_start_moving_position_mode; };
  Transition  { MovingSpeed;    Stoped;         ev_y_drive_sim_control;             g_y_drive_stop_request;           a_y_drive_sim_stop_moving;            };
  Transition  { MovingSpeed;    Stoped;         ev_y_drive_sim_control;             g_y_drive_mode_request_not_used;  a_y_drive_error_moving;               };

  Transition  { MovingPosition; MovingPosition; ev_y_drive_periodic_moving_timer;   g_y_drive_position_mode_request;  a_y_drive_moving_position_mode;       };
  Transition  { MovingPosition; MovingSpeed;    ev_y_drive_periodic_moving_timer;   g_y_drive_speed_mode_request;     a_y_drive_start_moving_speed_mode;    };
  Transition  { MovingPosition; Stoped;         ev_y_drive_sim_control;             g_y_drive_stop_request;           a_y_drive_sim_stop_moving;            };
  Transition  { MovingPosition; Stoped;         ev_y_drive_sim_control;             g_y_drive_mode_request_not_used;  a_y_drive_error_moving;               };
  Transition  { MovingPosition; Inpos;                                              g_y_drive_reach_inpos;                                                  };

  Transition  { Inpos;          Stoped;         ev_y_drive_sim_control;             g_y_drive_stop_request;           a_y_drive_sim_stop_moving;            };
};

Statemachine
{
  id  { sm_Y_drive_sim_brake; };
  description { "Y_drive_sim_brake"; };
  States  { Initial;  Applied; /*Released;*/ };
  import  { sm_Y_drive_sim_move;  };

  Actions
  {
    a_y_drive_brake_release
    {
      print ( "-------- Y_drive_sim brake is released  --------\n" );
      y_drive.Status.ANTRIEB_BRAKE_RELEASED = 1;
    };
    a_y_drive_brake_apply
    {
      print ( "-------- Y_drive_sim brake is applied  --------\n" );
      y_drive.Status.ANTRIEB_BRAKE_RELEASED = 0;
    };
  };
  on_enter  { a_y_drive_brake_apply; };
  on_exit   { a_y_drive_brake_apply; };

  Transition  { Initial;              Applied;              };
  Transition  { Applied;              sm_Y_drive_sim_move;  a_y_drive_brake_release;  g_y_drive_brake_release_request; };
  Transition  { sm_Y_drive_sim_move;  Applied;              a_y_drive_brake_apply;   !g_y_drive_brake_release_request; };
};

Statemachine
{
  id  { sm_Y_drive_sim_enable; };
  description { "Y_drive_sim_enable"; };
  States  { Initial;  Disabled; /*Enabled;*/ };
  import  { sm_Y_drive_sim_brake;  };

  Actions
  {
    a_y_drive_sim_is_enabled
    {
      print ( "-------- Y_drive_sim is enabled  --------\n" );
      y_drive.Status.ANTRIEB_ENABLE_OUT     = 1;
    };
    a_y_drive_sim_is_disabled
    {
      print ( "-------- Y_drive is disabled  --------\n" );
      y_drive.Status.ANTRIEB_ENABLE_OUT     = 0;
    };
  };
  on_enter  { a_y_drive_sim_is_disabled; };
  on_exit   { a_y_drive_sim_is_disabled; };

  Transition  { Initial;              Disabled;                                         g_y_drive_is_operational; };
  Transition  { Disabled;             sm_Y_drive_sim_brake; a_y_drive_sim_is_enabled;   g_y_drive_enable_request; };
  Transition  { sm_Y_drive_sim_brake; Disabled;             a_y_drive_sim_is_disabled; !g_y_drive_enable_request; };
};

Statemachine
{
  id  { sm_Y_drive_sim; };
  description { "Y_drive_sim"; };
  States  { Initial;  NotReady; /*Ready;*/  };
  import  { sm_Y_drive_sim_enable;  };

  Actions
  {
    a_y_drive_sim_is_ready
    {
      print ( "-------- Y_drive_sim is ready --------\n" );
      y_drive.Status.ANTRIEB_READY  = 1;
    };
    a_y_drive_sim_is_not_ready
    {
      print ( "-------- Y_drive_sim is not ready --------\n" );
      y_drive.Status.ANTRIEB_READY  = 0;
    };
  };
  on_enter
  {
    a_y_drive_sim_is_not_ready;
    start_periodic_timer ( DEF_ANTRIEBE_PERIODE * s, ev_y_drive_sim_state_response, timer_y_drive_sim_state_response );
  };
  on_exit
  {
    a_y_drive_sim_is_not_ready;
    stop_timer ( timer_y_drive_sim_state_response );
  };

  Transition  { Initial;                NotReady;               };
  Transition  { NotReady;               sm_Y_drive_sim_enable;  a_y_drive_sim_is_ready;      !g_y_drive_fault;  };
  Transition  { sm_Y_drive_sim_enable;  NotReady;               a_y_drive_sim_is_not_ready;   g_y_drive_fault;  };
};
