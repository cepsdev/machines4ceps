// X_drive
Event ev_x_drive_stop, ev_x_drive_move, ev_x_drive_position_changed, ev_x_drive_is_enabled;
Event ev_x_drive_move_up, ev_x_drive_move_down, ev_x_drive_enable, ev_x_drive_disable, ev_x_drive_is_disabled;

global_functions 
{
  a_set_x_drive_state_to_idle
  {
    ev_x_drive_stop;
  };
  a_x_drive_position_changed  { ev_x_drive_position_changed;  };

  x_drive_position_change
  {
    print ( "---------------- X_drive ---------------- IstPos   : ", x_drive.IstPos, " and SollPos     : ", x_drive.SollPos, "\n");
    if ( abs ( abs ( x_drive.IstPos ) - abs ( x_drive.SollPos ) ) < x_drive.ANGLE_TOLERANCE )
    {
      print ( "---------------- X_drive ---------------- event:      ", argv(0),  "\n" );
      a_set_x_drive_state_to_idle;
    } else 
    {
      if ( x_drive.IstPos < x_drive.SollPos )
      {
        x_drive.IstPos = x_drive.IstPos + 0.1;
        a_x_drive_position_changed;
      } else
      if ( x_drive.IstPos > x_drive.SollPos )
      {
        x_drive.IstPos = x_drive.IstPos - 0.1;
        a_x_drive_position_changed;
      };
    };
  };
};

Statemachine
{
  id  { sm_X_drive; };
  description { "X_drive"; };
  States  { Initial;  Idle; Disabled; Moving; };

  Actions
  {
    a_x_drive_start_moving
    {
//      stop_timer ( timer_x_drive_moving );
      if ( x_drive.SollGeschw >= 0.1 )
      {
        print ( "-------- X_drive starts moving with SollGeschw: ", x_drive.SollGeschw, " and SollPos: ", x_drive.SollPos, "\n" );
        start_periodic_timer ( ( 1.0 / x_drive.SollGeschw ) * s, x_drive_position_change, timer_x_drive_moving );
      } else
      {
        print ( "-------- X_drive SollGeschw == 0 ... do nothing" );
      };
    };
    a_x_drive_stop_moving
    {
      print ( "-------- X_drive stops moving --------\n" );
      stop_timer ( timer_x_drive_moving );
    };
    a_x_drive_is_enabled
    {
      print ( "-------- X_drive is enabled  --------\n" );
      ev_x_drive_is_enabled;
    };
    a_x_drive_is_disabled
    {
      print ( "-------- X_drive is disabled  --------\n" );
      ev_x_drive_is_disabled;
    };
  };

  on_enter
  {
    //print("-------- X_drive.on_enter(), SollGeschw: -------- ", x_drive.SollGeschw, "\n");
  };

  Transition  { Initial;  Disabled; };
  Transition  { Disabled; Idle;     ev_x_drive_enable;  a_x_drive_is_enabled;  !g_x_drive_has_antrieb_fault;  };
  Transition  { Idle;     Moving;   ev_x_drive_move;    a_x_drive_start_moving; };
  Transition  { Moving;   Idle;     ev_x_drive_stop;    a_x_drive_stop_moving;  };
  Transition  { Idle;     Disabled; ev_x_drive_disable; a_x_drive_is_disabled;  };
};
