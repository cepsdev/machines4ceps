// Y_drive
Event ev_y_drive_stop, ev_y_drive_move, ev_y_drive_position_changed, ev_y_drive_is_enabled;
Event ev_y_drive_move_up, ev_y_drive_move_down, ev_y_drive_enable, ev_y_drive_disable, ev_y_drive_is_disabled;

global_functions 
{
  a_set_y_drive_state_to_idle
  {
    ev_y_drive_stop;
  };
  a_y_drive_position_changed  { ev_y_drive_position_changed;  };

  y_drive_position_change
  {
    print ( "---------------- Y_drive ---------------- IstPos   : ", y_drive.IstPos, " - SollPos     : ", y_drive.SollPos, "\n" );
    if ( abs ( abs ( y_drive.IstPos ) - abs ( y_drive.SollPos ) ) < y_drive.ANGLE_TOLERANCE )
    {
      print ( "---------------- Y_drive ---------------- event:      ", argv(0),  "\n" );
      a_set_y_drive_state_to_idle;
    } else
    {
      if ( y_drive.IstPos < y_drive.SollPos )
      {
        y_drive.IstPos = y_drive.IstPos + 0.5;
        a_y_drive_position_changed;
      } else
      if ( y_drive.IstPos > y_drive.SollPos )
      {
        y_drive.IstPos = y_drive.IstPos - 0.5;
        a_y_drive_position_changed;
      };
    };
  };
};

Statemachine
{
  id  { sm_Y_drive; };
  description { "Y_drive"; };
  States  { Initial;  Idle; Disabled; Moving; };

  Actions
  {
    a_y_drive_start_moving
    {
//      stop_timer ( timer_y_drive_moving );
      if ( y_drive.SollGeschw >= 0.1 )
      {
        print ( "-------- Y_drive starts moving with SollGeschw: ", y_drive.SollGeschw, " and SollPos: ", y_drive.SollPos, "\n" );
        start_periodic_timer ( ( 1.0 / y_drive.SollGeschw ) * s, y_drive_position_change, timer_y_drive_moving );
      } else
      {
        print ( "-------- Y_drive SollGeschw == 0 ... do nothing" );
      };
    };
    a_y_drive_stop_moving
    {
      print ( "-------- Y_drive stops moving --------\n" );
      stop_timer ( timer_y_drive_moving );
    };
    a_y_drive_is_enabled
    {
      print ( "-------- Y_drive is enabled  --------\n" );
      ev_y_drive_is_enabled;
    };
    a_y_drive_is_disabled
    {
      print ( "-------- Y_drive is disabled  --------\n" );
      ev_y_drive_is_disabled;
    };
  };

  on_enter
  {
    //print("-------- Y_drive.on_enter(), SollGeschw: -------- ", y_drive.SollGeschw, "\n");
  };

  Transition  { Initial;  Disabled;                                             g_y_drive_is_operational; };
  Transition  { Disabled; Idle;     ev_y_drive_enable;  a_y_drive_is_enabled;  !g_y_drive_has_antrieb_fault;  };
  Transition  { Idle;     Moving;   ev_y_drive_move;    a_y_drive_start_moving; };
  Transition  { Moving;   Idle;     ev_y_drive_stop;    a_y_drive_stop_moving;  };
  Transition  { Idle;     Disabled; ev_y_drive_disable; a_y_drive_is_disabled;  };
};
