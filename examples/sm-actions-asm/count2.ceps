/*
Example for events without payload in actions written in Oblectamenta assembler.
*/

kind Event;
Event E,F;

oblectamenta{
 global{
   data{
        OblectamentaDataLabel i,n,one;
        i; 0; // counting index
        n; 100; // number of iterations
        one; 1; 
    };
 };
};

sm{
    CountingMachine;
    states{Initial;Count;Done;};
    Actions{
        doIncrementCounter{
            oblectamenta{
                        text{
                            asm{
                                OblectamentaDataLabel i, n, one;
                                OblectamentaCodeLabel done;

                                //Invariant: compute stack empty
                                ldi32(i); // push value of i to compute stack
                                ldi32(n); // push value of n to compute stack
                                blteq(done); // branch to done iff i >= n
                                //Invariant: compute stack empty 
                                ldi32(i); // push value of i to compute stack
                                ldi32(one); // push value of one == 1 to compute stack
                                addi32; // push i + 1 to compute stack
                                sti32(i); // store int32 value on top of compute stack to i
                                E; // fire event E => loop (see state machine's transition under event E)
                                halt;                                
                                done;
                                //Invariant: compute stack empty
                                F; // fire event F => terminate (see state machine's transition under event F)
                                halt;
                            };
                        };
            };
        };
        doPrintCounter{
            oblectamenta{
                        text{
                            asm{
                                OblectamentaDataLabel i, n, one;
                                dbg_printlni32(i);
                                halt;
                            };
                        };
            };
        };
    };
    t{Initial;Count;doIncrementCounter;};
    t{Count;Count;E;doIncrementCounter;};
    t{Count;Done;F;doPrintCounter;};
};

Simulation{
    Start{CountingMachine;};
};