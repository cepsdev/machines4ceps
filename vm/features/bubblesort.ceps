Algorithm{
  docinfo_se("BubbleSort");
  repeat{
    b = TRUE;
    for (i : range(1 ,N-1)) {
        if (a[i] > a[i+1]){
            b = FALSE;
            swap(a[i],a[i+1]);
        }
    }
    until{ b;};
  };
};

Scenario{
     title{label _ title = "Maximal element in vector.";};
     Given{
		let the_stack = obj(stack{});
        the_stack;
     };
     And{
		let the_data = obj(
            data{
                OblectamentaDataLabel array;
                OblectamentaDataLabel b;
                OblectamentaDataLabel n;
                OblectamentaDataLabel i;
                array; 4;1;7;9;2;5;8;10;3;6;
                i; 0;
                b; 0;
                n; 10;
            }
        );
        the_data;
     };
     And{
		let the_code = obj(
            text{
                OblectamentaDataLabel array;
                OblectamentaDataLabel b;
                OblectamentaDataLabel n;
                OblectamentaDataLabel i;
                OblectamentaCodeLabel done, outer_loop, inner_loop, inner_loop_end, no_swap;
                
                outer_loop;
                 ldi32(1);
                 sti32(b);

                 ldi32(0);
                 sti32(i);
                inner_loop;
                 ldi32(i);
                 ldi32(1);
                 ldi32(n);
                 subi32;
                 blteq(inner_loop_end);

                 lea(array);
                 ldi32(i);
                 ldi32(4);
                 muli32;
                 addi32; 
                 ldsi32; // stack[top] = array[i]

                 lea(array);                
                 ldi32(i);
                 ldi32(1);
                 addi32;
                 ldi32(4);
                 muli32;
                 addi32;
                 ldsi32;

                 bgteq(no_swap); 
                 ldi32(0);
                 sti32(b);

                 lea(array);
                 ldi32(i);
                 ldi32(4);
                 muli32;
                 addi32;
                 ldsi32;

                 lea(array);
                 ldi32(i);
                 ldi32(1);
                 addi32;
                 ldi32(4);
                 muli32;
                 addi32;
                 ldsi32;

                 lea(array);
                 ldi32(i);
                 ldi32(4);
                 muli32;
                 addi32;
                 swp;
                 stsi32;

                 lea(array);
                 ldi32(i);
                 ldi32(1);
                 addi32;
                 ldi32(4);
                 muli32;
                 addi32;
                 swp;
                 stsi32;

                no_swap;


                 ldi32(i);
                 ldi32(1);
                 addi32;
                 sti32(i);
                 buc(inner_loop);
                inner_loop_end;

                 ldi32(b);
                 bzeroi32(outer_loop);
                
                done; 
                 halt;
           });
        the_code;
     };
     When{
        let the_vm = run_oblectamenta_bytecode(
            vm{
                the_stack;
                the_data;
                the_code;
            }
        );
     };
     Then
     {
        input{
            the_stack;
            the_data;
        };
        output{
            the_vm.vm.stack;
            the_vm.vm.data;
        };
        verdict{
            equality_test{
                v{for( i : 1 .. 10){i; 0 ; 0; 0;}};
                v{for( i : 0 .. 9){the_vm.vm.data.content().at(i*4); 0 ; 0; 0;}};
            };
        };
     };
};

Summary{
 for( e : root.Scenario.Then.result){
  e;
 }
};